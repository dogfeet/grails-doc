Grails는 HTTP의 Accept 헤더, 명시적 포멧의 요청 파라미터(explicit format request parameter), 매핑된 URI의 확장자을 사용하여 [Content negotiation|http://en.wikipedia.org/wiki/Content_negotiation]을 지원한다.

h4. Configuring Mime Types(마임타입 설정하기)

Content Negotiation을 처리하는 법을 다루기 전에 Grails에 어떤 컨텐트 타입에 적용되길 바라는지 알려주어야 한다. Grails는 기본적으로 grails-app/conf/Config.groovy 파일의 grails.mime.types에 많은 컨텐트 타입이 설정되어 있다:

{code:java}
grails.mime.types = [ xml: ['text/xml', 'application/xml'],
                      text: 'text-plain',
                      js: 'text/javascript',
                      rss: 'application/rss+xml',
                      atom: 'application/atom+xml',
                      css: 'text/css',
                      cvs: 'text/csv',
                      all: '*/*',
                      json: 'text/json',
                      html: ['text/html','application/xhtml+xml']
                    ]
{code}

이 설정은 Grails가 'text/xml, 'application/xml'을 포함하는 요청 형식을 xml로 감지할 수 있게 해준다. Grail는 단순히 맵에 새로운 내용을 추가하는 것만으로 새로운 컨텐트 타입을 감지할 수 있다.

h4. Content Negotiation using the Accept header(Accept 헤더를 사용한 Content Negotiation)

모든 HTTP 요청에는 무슨 미디어 타입(마임 타입이나)인지를 정의한 [Accept|http://www.w3.org/Protocols/rfc2616/rfc2616-sec14.html] 헤더가 있다. 이 헤더는 클라이언트에서 받아 들일 수 있는 형식을 의미한다. 예전 브라우져들의 Accept 헤더 내용은 일반적으로 다음과 같다:

{code:java}
*/*
{code}

이 것은 단순히 모든 것을 의미한다. 하지만 요즘의 브라우져들은 다음과 같이 좀 더 유용한 정보를 보낸다(다음의 예는 Firefox의 Accept 헤더이다):

{code:java}
text/xml,application/xml,application/xhtml+xml,text/html;q=0.9,text/plain;q=0.8,image/png,*/*;q=0.5
{code}

Grails는 이 정보를 파싱하고 최선의(prefered) [request|servletAPI] 형식을 요청 객체의 프로퍼티에 추가한다. 이 예제는 다음과 같은 assert문을 통과할 것이다:

{code:java}
assert 'html' == request.format
{code}

text/html 미디어 타입은 “quality” 비율이 0.9이다. 따라서 가장 우선순위가 높다. 예전 브라우저를 사용하고 있다면 약간 다른 결과를 얻게 될 것이다:

{code:java}
assert 'all' == request.format
{code}

이 경우 클라이언트가 '모든' 포멧을 수용할 수 있다는 것을 의미한다. 다양한 요청을 처리하기 위해 컨트롤러([Controllers|guide:controllers])에서 [withFormat|controllers] 메소드를 사용한다. 이 메소드는 switch문처럼 동작한다:

{code:java}
import grails.converters.*

class BookController {
	def books
	def list = {
		this.books = Book.list()
		withFormat {
			html bookList:books
			js { render "alert('hello')" } 
			xml { render books as XML }
		}
	}
}
{code}

최선의 포멧이 html이라면 Grails는 html()만 호출 할 것이다. 이 것은 Grails가 grails-app/views/books/list.html.gsp나 grails-app/views/books/list.gsp를 찾도록 만든다. 만약 이 포멧이 xml이라면 클로저가 실행되서 XML로 응답한다.

예전 브라우저들처럼 Accept 헤더가 모든 형식인 경우에는 withFormat 메소드에 정의한 순서대로 포멧이 선택되어 호출된다. 위의 예제에서는 html 메소드가 먼저 실행될 것이다.

{note}
액션의 [withFormat|controllers] 메소드가 반환하는 값이 다음에 무엇을 해야하는지를 의미하기 때문에 컨트롤러의 액션에서 마지막에 무엇이 호출되는지를 분명하게 해야 한다.
{note}

h4. Content Negotiation with the format Request Parameter(포멧 요청 파라미터를 사용하는 Content Negotiation)

요청 헤더를 처리하는 것이 싫다면 덮어쓰도록(override) 요청 파라미터에 다음과 같은 형식으로 명시할 수 있다:

{code:java}
/book/list?format=xml
{code}

그리고 이 파라미터를 URL 매핑([URL Mappings|guide:mappings])에도 정의할 수 있다:

{code:java}
"/book/list"(controller:"book", action:"list") {
	format = "xml"
}
{code}

h4. Content Negotiation with URI Extensions(URI의 확장자를 이용한 Content Negotiation)

Grails는 URI의 확장자를 이용한 Content Negotiation도 지원한다. 예를 들어 다음과 같은 URI가 있을 때:

{code:java}
/book/list.xml
{code}

Grails는 확장자에 따라 컨텐트 포멧을 xml로 설정하고 확장자를 잘라버린 후 /book/list 메소드에 매핑시킨다. 이 것은 아무것도 설정하지 않고 기본적으로 사용가능하다. 이 기능을 끄고 싶다면 grails-app/conf/Config.groovy 파일의 grails.mime.file.extensions 프로퍼티를 false로 설정해야 한다:

{code:java}
grails.mime.file.extensions = false
{code}

h4. Testing Content Negotiation(Content Negotiation 테스트하기)

통합(integration) 테스트시 요청 헤더를 조작하여 Content Negotiation을 테스트([Testing|guide:testing])할 수 있다:

{code:java}
void testJavascriptOutput() {
	def controller = new TestController()
	controller.request.addHeader "Accept", "text/javascript, text/html, application/xml, text/xml, */*"
	
	controller.testAction()
	assertEquals "alert('hello')", controller.response.contentAsString
}
{code} 

다른 방법으로 format 파라미터를 직접 설정하여 동일한 효과를 낼 수 있다:

{code:java}
void testJavascriptOutput() {
	def controller = new TestController()
	controller.params.format = 'js'
	
	controller.testAction()
	assertEquals "alert('hello')", controller.response.contentAsString
}
{code}
